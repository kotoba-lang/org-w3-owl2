(ns owl.reason-test
  (:require #?(:clj [clojure.test :refer [deftest is testing]]
               :cljs [cljs.test :refer-macros [deftest is testing]])
            [owl.model :as m]
            [owl.reason :as r]))

(def a (m/class "http://ex.org#A"))
(def b (m/class "http://ex.org#B"))
(def c (m/class "http://ex.org#C"))
(def x (m/named-individual "http://ex.org#x"))

(deftest subclass-transitive-closure
  (testing "A subClassOf B subClassOf C -- C is a transitive superclass of A"
    (let [o (-> (m/ontology "http://ex.org")
                (m/add-axiom (m/sub-class-of a b))
                (m/add-axiom (m/sub-class-of b c)))]
      (is (= #{"http://ex.org#B" "http://ex.org#C"} (r/superclasses-of o "http://ex.org#A")))
      (is (r/subclass-of? o "http://ex.org#A" "http://ex.org#C"))
      (is (not (r/subclass-of? o "http://ex.org#C" "http://ex.org#A"))))))

(deftest class-assertion-transitive-membership-is-derivable
  (testing "individual x asserted an A; A subClassOf B subClassOf C => C is
  in x's inferred-types even without a direct ClassAssertion of x as C"
    (let [o (-> (m/ontology "http://ex.org")
                (m/add-axiom (m/sub-class-of a b))
                (m/add-axiom (m/sub-class-of b c))
                (m/add-axiom (m/class-assertion a x)))]
      (is (= #{"http://ex.org#A"} (r/asserted-types o "http://ex.org#x")))
      (is (= #{"http://ex.org#A" "http://ex.org#B" "http://ex.org#C"}
             (r/inferred-types o "http://ex.org#x")))
      (is (contains? (r/inferred-types o "http://ex.org#x") "http://ex.org#C")))))

(def p (m/object-property "http://ex.org#p"))
(def n1 (m/named-individual "http://ex.org#n1"))
(def n2 (m/named-individual "http://ex.org#n2"))
(def n3 (m/named-individual "http://ex.org#n3"))
(def n4 (m/named-individual "http://ex.org#n4"))

(deftest transitive-property-closure
  (testing "a chain of asserted facts under a TransitiveObjectProperty closes over reachability"
    (let [o (-> (m/ontology "http://ex.org")
                (m/add-axiom (m/transitive-object-property p))
                (m/add-axiom (m/object-property-assertion p n1 n2))
                (m/add-axiom (m/object-property-assertion p n2 n3))
                (m/add-axiom (m/object-property-assertion p n3 n4)))
          facts (r/materialize o)]
      (is (contains? facts ["http://ex.org#p" "http://ex.org#n1" "http://ex.org#n2"]))
      (is (contains? facts ["http://ex.org#p" "http://ex.org#n1" "http://ex.org#n3"]))
      (is (contains? facts ["http://ex.org#p" "http://ex.org#n1" "http://ex.org#n4"]))
      (is (r/entails-object-property-fact? o "http://ex.org#p" "http://ex.org#n1" "http://ex.org#n4"))
      (is (not (r/entails-object-property-fact? o "http://ex.org#p" "http://ex.org#n4" "http://ex.org#n1"))))))

(deftest symmetric-property-materialization
  (let [o (-> (m/ontology "http://ex.org")
              (m/add-axiom (m/symmetric-object-property p))
              (m/add-axiom (m/object-property-assertion p n1 n2)))
        facts (r/materialize o)]
    (is (contains? facts ["http://ex.org#p" "http://ex.org#n1" "http://ex.org#n2"]))
    (is (contains? facts ["http://ex.org#p" "http://ex.org#n2" "http://ex.org#n1"]))))

(def q (m/object-property "http://ex.org#q"))

(deftest inverse-property-materialization
  (let [o (-> (m/ontology "http://ex.org")
              (m/add-axiom (m/inverse-object-properties p q))
              (m/add-axiom (m/object-property-assertion p n1 n2)))
        facts (r/materialize o)]
    (is (contains? facts ["http://ex.org#p" "http://ex.org#n1" "http://ex.org#n2"]))
    (is (contains? facts ["http://ex.org#q" "http://ex.org#n2" "http://ex.org#n1"]))))
